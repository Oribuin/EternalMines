plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1' apply false
    id 'maven-publish'
}

allprojects {
    apply plugin: 'java-library'
    apply plugin: 'com.github.johnrengelman.shadow'
    group = 'xyz.oribuin'
    version = '1.0.1'

    java {
        toolchain.languageVersion.set(JavaLanguageVersion.of(17))
    }

    repositories {
        mavenCentral()
        mavenLocal()

        maven { url = "https://libraries.minecraft.net" }
        maven { url = "https://repo.papermc.io/repository/maven-public/" }
        maven { url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
        maven { url = "https://repo.rosewooddev.io/repository/public/" }
        maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
        maven { url = "https://repo.codemc.org/repository/maven-public/" }
        maven { url = "https://jitpack.io" }
        maven { url = "https://repo.mattstudios.me/artifactory/public/" }
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:24.0.1'
    }

}

dependencies {
    implementation project(':Plugin')
    implementation project(':NMS:Wrapper')
    implementation project(path: ':NMS:v1_17_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_18_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R2', configuration: 'remapped')
    implementation project(path: ':NMS:v1_19_R3', configuration: 'remapped')
    implementation project(path: ':NMS:v1_20_R1', configuration: 'remapped')
    implementation project(path: ':NMS:v1_20_R2', configuration: 'remapped')
}
shadowJar {
    archiveClassifier.set(null)

    relocate("dev.rosewood.rosegarden", "${project.group}.eternalmines.rosegarden")

    exclude "dev/rosewood/rosegarden/lib/hikaricp/**/*.class"
    exclude "dev/rosewood/rosegarden/lib/slf4j/**/*.class"
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
            artifactId = 'eternalmines'
            pom {
                name = 'eternalmines'
            }
        }
    }
    repositories {
        if (project.hasProperty('mavenUser') && project.hasProperty('mavenPassword')) {
            maven {
                credentials {
                    username project.mavenUser
                    password project.mavenPassword
                }

                def releasesRepoUrl = 'https://repo.rosewooddev.io/repository/public-releases/'
                def snapshotsRepoUrl = 'https://repo.rosewooddev.io/repository/public-snapshots/'
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

build.dependsOn shadowJar
